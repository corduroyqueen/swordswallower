/*-----------------------------------------------------------------------------
Brightness, Contrast & Saturation Shader
Fragment Shader: Apply Brightness, Contrast & Saturation
-------------------------------------------------------------------------------
_______________________________________________________________________________
INFO:
-------------------------------------------------------------------------------
The code of the function "ContrastSaturationBrightness"
is from TGM's shader pack available here:
http://irrlicht.sourceforge.net/phpBB2/viewtopic.php?t=21057
-----------------------------------------------------------------------------*/

varying vec2 v_vTexcoord;
varying vec4 v_vColour;

uniform float brightness;
uniform float contrast;
uniform float saturation;

// For all settings: 1.0 = 100% 0.5=50% 1.5 = 150%
vec3 ContrastSaturationBrightness(vec3 color, float brt, float sat, float con)
{
	// Increase or decrease theese values to adjust r, g and b color channels seperately
	const float AvgLumR = 0.2;
	const float AvgLumG = 0.2;
	const float AvgLumB = 0.2;
	
	const vec3 LumCoeff = vec3(0.2125, 0.7154, 0.0721);
	
	vec3 AvgLumin = vec3(AvgLumR, AvgLumG, AvgLumB);
	vec3 brtColor = color * brt;
	vec3 intensity = vec3(dot(brtColor, LumCoeff));
	vec3 satColor = mix(intensity, brtColor, sat);
	vec3 conColor = mix(AvgLumin, satColor, con);
	return conColor;
}


void main()
{
    vec4 base_col = v_vColour * texture2D( gm_BaseTexture, v_vTexcoord );
	gl_FragColor = vec4(ContrastSaturationBrightness(base_col.rgb, brightness, saturation, contrast), base_col.a);
}

